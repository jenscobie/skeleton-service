import com.bmuschko.gradle.vagrant.tasks.Vagrant

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

plugins {
    id 'application'
    id 'com.bmuschko.vagrant' version '2.0'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'groovy'
    id 'idea'
    id 'nebula.os-package' version '2.2.6'
    id 'sonar-runner'
}

project.buildDir = 'target'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.8.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

sourceSets {
    acceptanceTest {
        groovy.srcDir "src/acceptance/groovy"
        resources.srcDir "src/acceptance/resources"
        compileClasspath += test.compileClasspath
        runtimeClasspath += test.runtimeClasspath
    }
}

idea {
    module {
        testSourceDirs += file('src/contract/groovy')
    }
    project {
        ipr {
            withXml { provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://192.168.50.5:9000"
        property "sonar.jdbc.url", "jdbc:h2:tcp://192.168.50.5:9092/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
    }
}

mainClassName = 'com.example.helloworld.HelloWorldApplication'

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

run {
    args 'server', './src/main/resources/hello-world.yml'
}

shadowJar {
    archiveName 'skeleton-service.jar'
    mergeServiceFiles()
}

ospackage {
    version = '1.0.0'
    release = 'SNAPSHOT'
    arch = X86_64
    os = LINUX

    into '/opt/skeleton-service'

    from(jar.outputs.files) {
        into 'lib'
    }

    from('src/main/resources') {
        fileType CONFIG | NOREPLACE
        into 'conf'
    }

    from('provision') {
        into 'bin'
        fileMode = 0755
    }

    link('/etc/init.d/skeleton-service', '/opt/skeleton-service/bin/skeleton-service')
}

def DEPLOYMENT = 'Deployment'

buildRpm.dependsOn shadowJar

task setup(type: Exec) {
    commandLine 'ansible-galaxy', 'install', '--role-file=Galaxyfile', '--roles-path=provision/roles', '--force'
}

task dist(dependsOn: buildRpm) {
    group DEPLOYMENT
    description 'Create an RPM package with application and infrastructure code'
}

task packageZip(type: Zip, dependsOn: dist) {
    from('target/distributions')
    destinationDir file('dist')
}

task boot(type: Vagrant) {
    group DEPLOYMENT
    description 'Ensure a local virtual machine is running'
    commands = ['up', '--no-provision']
}

task deploy(type: Vagrant, dependsOn: dist) {
    group DEPLOYMENT
    description = 'Deploy snapshot to local virtual machine'
    commands = ['provision']
}

task destroy(dependsOn: vagrantDestroy) {
    group DEPLOYMENT
    description 'Destroy the currently running local virtual machine'
}

task acceptance(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task precommit(dependsOn: [clean, test, boot, acceptance]) {
    group 'Verification'
    description 'Run all validations before pushing code'
}

test.mustRunAfter clean
boot.mustRunAfter test
acceptance.mustRunAfter boot

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}