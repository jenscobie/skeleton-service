import com.bmuschko.gradle.vagrant.tasks.Vagrant

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

plugins {
    id 'application'
    id 'com.bmuschko.vagrant' version '2.0'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'idea'
    id 'nebula.os-package' version '2.2.6'
    id 'sonar-runner'
}

project.buildDir = 'target'

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.8.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.typesafe:config:1.2.1'
    testCompile 'com.jayway.restassured:rest-assured:2.4.1'
    testCompile 'com.jayway.restassured:json-path:2.4.1'
}

sourceSets {
    acceptanceTest {
        java.srcDir "src/acceptance/java"
        resources.srcDir "src/acceptance/resources"
        compileClasspath += test.compileClasspath
        runtimeClasspath += test.runtimeClasspath
    }
}

idea {
    project {
        ipr {
            withXml { provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://192.168.50.5:9000"
        property "sonar.jdbc.url", "jdbc:h2:tcp://192.168.50.5:9092/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
    }
}

mainClassName = 'com.example.helloworld.HelloWorldApplication'

jar {
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

run {
    args 'server', './src/main/resources/hello-world.yml'
}

shadowJar {
    archiveName 'skeleton-service.jar'
    mergeServiceFiles()
}

ospackage {
    version = '1.0.0'
    release = 'SNAPSHOT'
    arch = X86_64
    os = LINUX

    into '/opt/skeleton-service'

    from(jar.outputs.files) {
        into 'lib'
    }

    from('src/main/resources') {
        fileType CONFIG | NOREPLACE
        into 'conf'
    }

    from('provision') {
        into 'bin'
        fileMode = 0755
    }

    link('/etc/init.d/skeleton-service', '/opt/skeleton-service/bin/skeleton-service')
}

task setup(type: Exec) {
    commandLine 'ansible-galaxy', 'install', '--role-file=Galaxyfile', '--roles-path=provision/roles', '--force'
}

buildRpm.dependsOn shadowJar

def PACKAGE = 'Package'

task dist(dependsOn: buildRpm) {
    group PACKAGE
    description 'Create an RPM package with application and infrastructure code'
}

task packageZip(type: Zip, dependsOn: dist) {
    group PACKAGE
    from('target/distributions')
    destinationDir file('provision/roles/jenscobie.skeleton-service/dist')
}

def DEPLOYMENT = 'Deployment'

task boot(type: Vagrant) {
    group DEPLOYMENT
    description 'Ensure a local virtual machine is running'
    commands = ['up', '--no-provision']
}

task deploy(type: Vagrant, dependsOn: packageZip) {
    group DEPLOYMENT
    description = 'Deploy snapshot to local virtual machine'
    commands = ['provision']
}

task destroy(dependsOn: vagrantDestroy) {
    group DEPLOYMENT
    description 'Destroy the currently running local virtual machine'
}

task acceptance(type: Test) {
    systemProperty "environment", System.getProperty("environment")

    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath

    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

task precommit(dependsOn: [clean, test, boot, deploy, acceptance]) {
    group 'Verification'
    description 'Run all validations before pushing code'
}

test.mustRunAfter clean
boot.mustRunAfter test
deploy.mustRunAfter boot
acceptance.mustRunAfter deploy

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}