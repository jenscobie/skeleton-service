buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-vagrant-plugin:2.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

plugins {
    id 'com.bmuschko.vagrant' version '2.0'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
    id 'groovy'
    id 'idea'
    id 'sonar-runner'
}

project.buildDir = 'target'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.8.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

sourceSets {
    contractTest {
        groovy.srcDir "src/contract/groovy"
        resources.srcDir "src/contract/resources"
        compileClasspath += test.compileClasspath
        runtimeClasspath += test.runtimeClasspath
    }
}

idea {
    module {
        testSourceDirs += file('src/contract/groovy')
    }
    project {
        ipr {
            withXml { provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://192.168.50.5:9000"
        property "sonar.jdbc.url", "jdbc:h2:tcp://192.168.50.5:9092/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
    }
}

jar {
    manifest {
        attributes("Main-Class": "com.example.helloworld.HelloWorldApplication")
    }
}

task boot(dependsOn: vagrantUp)
task destroy(dependsOn: vagrantDestroy)

task contract(type: Test) {
    testClassesDir = sourceSets.contractTest.output.classesDir
    classpath = sourceSets.contractTest.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}